export TEMP=$(mktemp -u)
trap 'rm -f "$TEMP"' EXIT

local _preview_cmd
if (( $+commands[bat] )); then
  _preview_cmd="bat --theme $(_bat_auto_theme) --color=always {1} --highlight-line {2}"
else
  _preview_cmd="cat {1}"
fi

local transformer='
  rg_pat={q:1}      # The first word is passed to ripgrep
  fzf_pat={q:2..}   # The rest are passed to fzf

  if ! [[ -r "$TEMP" ]] || [[ $rg_pat != $(cat "$TEMP") ]]; then
    echo "$rg_pat" > "$TEMP"
    printf "reload:sleep 0.1; rg --column --line-number --no-heading --color=always --smart-case %q || true" "$rg_pat"
  fi
  echo "+search:$fzf_pat"
'
fzf --ansi --disabled --query "${*:-}" \
  --with-shell 'bash -c' \
  --bind "start,change:transform:$transformer" \
  --color "hl:-1:underline,hl+:-1:underline:reverse" \
  --delimiter : \
  --preview $_preview_cmd \
  --preview-window 'up,60%,border-line,+{2}+3/3,~3' \
  --bind 'enter:become($EDITOR {1} +{2})'

# vim: ft=zsh
